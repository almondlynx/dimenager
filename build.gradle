plugins {
    id 'fabric-loom' version '0.6-SNAPSHOT'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = 'dimenager'
group = 'net.regnormc'
version = '0.0.2'

Set<String> apiModules = [
        "fabric-api-base",
        "fabric-resource-loader-v0"
]

dependencies {
    minecraft 'com.mojang:minecraft:1.16.5'
    mappings "net.fabricmc:yarn:1.16.5+build.6:v2"
    modImplementation "net.fabricmc:fabric-loader:0.11.3"

    apiModules.forEach {
        include(modImplementation(fabricApi.module(it, "0.32.5+1.16")))
    }
}

minecraft.accessWidener = file("src/main/resources/dimenager.accesswidener")

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    def targetVersion = 8
    if (JavaVersion.current().isJava9Compatible()) {
        it.options.release = targetVersion
    }
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}
